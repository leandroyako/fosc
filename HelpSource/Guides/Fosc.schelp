TITLE:: Fosc
SUMMARY::  Fosc is a SuperCollider API for generating musical notation in LilyPond.
CATEGORIES:: Fosc
â€‹
SECTION:: Fosc
EMPHASIS::Fosc:: stands for FO-rmalised S-core C-ontrol (FO-r S-uperC-ollider). It's a close relative of Abjad LINK::http://abjad.mbrsi.org::, and ports much of Abjad's Python code base to SuperCollider.

EMPHASIS::Fosc:: is a SuperCollider API for generating musical notation in LilyPond link::http://lilypond.org/::

EMPHASIS::Fosc:: lets you:
 LIST::
##Create musical notation in an object-oriented way
##Generate and transform complex rhythms through rhythm-makers and meter-rewriting
##Construct powerful component selectors and iterators for transforming musical objects in a score
##Control all of the typographic details of music notation
##Play musical scores through scsynth and MIDI
::

SECTION:: Fosc Installation

SUBSECTION:: 1. Install LilyPond

LilyPond is an open-source program that engraves music notation in an automated way. EMPHASIS::Fosc:: uses LilyPond to produce notational output. It's recommended that you install the most recent version of LilyPond directly from the LilyPond website. After you install LilyPond, check to see if LilyPond is callable from your command line.
CODE::
~$ lilypond --version

GNU LilyPond 2.19.83

Copyright (c) 1996--2015 by Han-Wen Nienhuys <hanwen@xs4all.nl> Jan Nieuwenhuizen <janneke@gnu.org> and others.

This program is free software.  It is covered by the GNU General Public License and you are welcome to change it and/or distribute copies of it under certain conditions.  Invoke as `lilypond --warranty` for more information.
::

SECTION:: 2. Install Fosc

Download the EMPHASIS::fosc:: master branch and unzip. Rename the 'fosc-master' folder to 'fosc'. Move the 'fosc' folder to your SuperCollider Extensions directory. Information on installing SuperCollider extensions can be found here: LINK::https://doc.sccode.org/Guides/UsingExtensions.html::


SECTION:: 3. Configure Fosc

In your sclang startup file, add code to allow EMPHASIS::Fosc:: to communicate with LilyPond. EMPHASIS::Note::: it's possible that your LilyPond binary may be installed somewhere different to the standard locations below.

EMPHASIS::Mac OS X::
CODE::
FoscConfiguration.lilypondExecutablePath = "/Applications/LilyPond.app/Contents/Resources/bin/lilypond";
::

EMPHASIS::Linux::
CODE::
FoscConfiguration.lilypondExecutablePath = "/usr/local/bin/lilypond";
::

Once you've saved your changes, recompile the SuperCollider class library and test that EMPHASIS::Fosc:: is able to call LilyPond.

CODE::
FoscConfiguration.getLilypondVersionString;
::

CODE::
2.19.82
::


SECTION:: Basic usage

SUBSECTION:: 1. Displaying music

Display a note.
CODE::
a = FoscNote(60, 1/4);
a.show;
::
IMAGE::img/note.png::

Display some notes in sequence.
CODE::
a = FoscVoice([FoscNote('C4', 1/4), FoscNote('D4', 1/8)]);
a.show;
::
IMAGE::img/notes.png::

View LilyPond output in the Post window.
CODE::
a = FoscVoice([FoscNote('C4', 1/4), FoscNote('D4', 1/8)]);
a.format;
::

CODE::
\new Voice {
    c'4
    d'8
}
::


Display a score.
CODE::
a = FoscVoice([FoscNote('C4', 1/4), FoscNote('D4', 1/8)]);
b = FoscVoice([FoscNote('Bb3', 1/8), FoscNote('Ab3', 1/4)]);
c = FoscScore([FoscStaff([a]), FoscStaff([b])]);
c.show;
::
IMAGE::img/score.png::


SUBSECTION:: 2. Indicators

Indicators attach to leaves.
CODE::
a = FoscNote(60, 1/4);
a.attach(FoscArticulation('>'));
a.attach(FoscDynamic('f'));
a.show;
::
IMAGE::img/indicators.png::


SUBSECTION:: 3. Spanners

Spanners attach to two or more contiguous leaves.
CODE::
a = FoscStaff(FoscLeafMaker().(#[60,62,64,65], [1/8]));
a.selectLeaves.slur;
a.selectLeaves.hairpin('p < f');
a.show;
::
IMAGE::img/spanners.png::


SUBSECTION:: 4. Tweaks, overrides, settings

Tweak LilyPond Grob properties.
CODE::
a = FoscNote(60, 1/4);
tweak(a.noteHead).style = 'harmonic';
a.show;
::
IMAGE::img/tweak-notehead.png::

Override LilyPond Grob properties.
CODE::
a = FoscNote(60, 1/4);
override(a).noteHead.color = 'red';
a.show;
::
IMAGE::img/override-notehead.png::

Set LilyPond Context properties.
CODE::
a = FoscScore([FoscStaff(FoscLeafMaker().(#[60,62,64,65], 1/8))]);
set(a[0]).instrumentName = FoscMarkup("Violin");
set(a).proportionalNotationDuration = FoscSchemeMoment(#[1,64]);
a.show;
::
IMAGE::img/set-score-properties.png::


SUBSECTION:: 5. Generate music with FoscLeafMaker

Integer, String or Symbol elements in 'pitches' slot result in notes.
CODE::
m = FoscLeafMaker().(pitches: #[60,64,"G4",'Bb4'], durations: [1/4]);
m.show;
::
IMAGE::img/leaf-maker-1.png::

Array elements in 'pitches' result in chords.
CODE::
m = FoscLeafMaker().(pitches: #[[60,64,67],['Eb4','G4','Bb4']], durations: [1/2]);
m.show;
::
IMAGE::img/leaf-maker-2.png::

Nil-valued elements in 'pitches' result in rests.
CODE::
m = FoscLeafMaker().(pitches: nil, durations: 1/4 ! 4);
m.show;
::
IMAGE::img/leaf-maker-3.png::

Values passed to 'pitches' can be mixed and matched.
CODE::
m = FoscLeafMaker().(pitches: #[[60,64,67],nil,'Eb4','Bb4'], durations: [1/4]);
m.show;
::
IMAGE::img/leaf-maker-4.png::

Pitches are repeated cyclically when the length of 'pitches' is less than the length of 'durations'.
CODE::
m = FoscLeafMaker().(pitches: #[72,71], durations: [3/8,1/8,2/8,2/8]);
m.show;
::
IMAGE::img/leaf-maker-5.png::

Durations are repeated cyclically when the length of 'durations' is less than the length of 'pitches'.
CODE::
m = FoscLeafMaker().(pitches: #[72,74,76,77], durations: [3/8,1/8]);
m.show;
::
IMAGE::img/leaf-maker-6.png::

Elements in 'durations' with non-power-of-two denominators result in tuplet-nested leaves.
CODE::
m = FoscLeafMaker().(pitches: #[60,62,64,65], durations: [1/4, 1/12, 1/6, 1/2]);
m.show;
::
IMAGE::img/leaf-maker-7.png::


SUBSECTION:: 6. Generate rhythms with FoscRhythmMaker

'Ratios' embed into 'divisions' as rhythmic proportions.
CODE::
a = FoscRhythmMaker();
a.(divisions: [2/16,3/16,5/16], ratios: #[[3,1],[3,2],[4,3]]);
a.show;
::
IMAGE::img/rhythm-maker-1.png::

Negative values in 'divisions' result in rests.
CODE::
a = FoscRhythmMaker();
a.(divisions: [2/16,3/16,5/16], ratios: #[[-3,1],[3,2],[4,-3]]);
a.show;
::
IMAGE::img/rhythm-maker-2.png::

Add the output of a FoscRhythmMaker to a FoscStaff and rewrite pitches.
CODE::
a = FoscRhythmMaker();
b = a.(divisions: [2/16,3/16,5/16], ratios: #[[-3,1],[3,2],[4,-3]]);
c = FoscStaff(b);
mutate(c).rewritePitches(#[60,62,64,65]);
c.show;
::
IMAGE::img/rhythm-maker-3.png::

Rhythm cells are repeated cyclically when the length of 'divisions' is less than the length of 'ratios'.
CODE::
a = FoscRhythmMaker();
a.(divisions: [1/4], ratios: #[1,1,1,1,1] ! 4);
a.show;
::
IMAGE::img/rhythm-maker-4.png::

Rhythm cells are repeated cyclically when the length of 'ratios' is less than the length of 'divisions'.
CODE::
a = FoscRhythmMaker();
a.(divisions: 1/4 ! 4, ratios: #[[1,1,1,1,1]]);
a.show;
::
IMAGE::img/rhythm-maker-5.png::

Apply a silence mask.
CODE::
m = FoscSilenceMask(FoscPattern(indices: #[0,1,4,5,17,18,19]));
a = FoscRhythmMaker();
a.(divisions: 1/4 ! 4, ratios: #[[1,1,1,1,1]], masks: [m]);
a.show;
::
IMAGE::img/rhythm-maker-6.png::

Apply a sustain mask.
CODE::
m = FoscSustainMask(FoscPattern(indices: #[0,1,4,5,17,18,19]));
a = FoscRhythmMaker();
a.(divisions: 1/4 ! 4, ratios: #[[1,1,1,1,1]], masks: [m]);
a.show;
::
IMAGE::img/rhythm-maker-7.png::

Apply a sustain mask and fuse leaves between successive onsets.
CODE::
m = FoscSustainMask(FoscPattern(indices: #[0,1,4,5,17,18,19]), fuse: true);
a = FoscRhythmMaker();
a.(divisions: 1/4 ! 4, ratios: #[[1,1,1,1,1]], masks: [m]);
a.show;
::
IMAGE::img/rhythm-maker-8.png::

Apply a sustain mask, fuse leaves, and apply formatting rules with a FoscTupletSpecifier.
CODE::
m = FoscSustainMask(FoscPattern(indices: #[0,1,4,5,17,18,19]), fuse: true);
t = FoscTupletSpecifier(extractTrivial: true, rewriteSustained: true, rewriteRestFilled: true);
a = FoscRhythmMaker(tupletSpecifier: t);
a.(divisions: 1/4 ! 4, ratios: #[[1,1,1,1,1]], masks: [m]);
a.show;
::
IMAGE::img/rhythm-maker-9.png::


SUBSECTION:: 7. Selections

Select all score components.
CODE::
a = FoscStaff([FoscRest(1/4), FoscNote(60, 1/4), FoscNote(62, 1/4)]);
b = a.selectComponents;
b.do { |each| each.cs.postln };
::

CODE::
FoscStaff([  ], 'Staff', false)
FoscRest(1/4)
FoscNote('C4', 1/4)
FoscNote('D4', 1/4)
::

Select notes and rests only.
CODE::
a = FoscStaff([FoscRest(1/4), FoscNote(60, 1/4), FoscNote(62, 1/4)]);
b = a.selectComponents(prototype: [FoscNote, FoscRest]);
b.do { |each| each.cs.postln };
::

CODE::
FoscRest(1/4)
FoscNote('C4', 1/4)
FoscNote('D4', 1/4)
::

Select pitched leaves only.
CODE::
a = FoscStaff([FoscRest(1/4), FoscNote(60, 1/4), FoscChord(#[60,64,67], 1/4)]);
b = a.selectLeaves(pitched: true);
b.do { |each| each.cs.postln };
::

CODE::
FoscNote('C4', 1/4)
FoscChord("C4 E4 G4", 1/4)
::


Attach indicators to all selected pitched leaves.
CODE::
a = FoscStaff(FoscLeafMaker().(#[nil,62,64,65,67,69,71,72], [1/8]));
b = a.selectLeaves(pitched: true);
b.do { |each| each.attach(FoscArticulation('>')) };
a.show;
::
IMAGE::img/selections-1.png::


SUBSECTION:: 8. Iteration

Iterate over all score components.
CODE::
a = FoscStaff(FoscLeafMaker().(#[60,62,64,65,67,69,71,72], [1/8]));
a.doComponents({ |each| each.cs.postln });
::

CODE::
FoscStaff([  ], 'Staff', false)
FoscNote('C4', 1/8)
FoscNote('D4', 1/8)
FoscNote('E4', 1/8)
FoscNote('F4', 1/8)
FoscNote('G4', 1/8)
FoscNote('A4', 1/8)
FoscNote('B4', 1/8)
FoscNote('C5', 1/8)
::


Iterate over notes, attach indicators.
CODE::
a = FoscStaff(FoscLeafMaker().(#[nil,62,64,65,67,69,71,72], [1/8]));
a.doComponents({ |note| note.attach(FoscArticulation('>')) }, prototype: FoscNote);
a.show;
::
IMAGE::img/iteration-1.png::

Iterate over pitched logical ties.
CODE::
a = FoscStaff(FoscLeafMaker().(#[60,60,62,nil,64,64], [1/4,1/24,1/12,1/8,1/4,1/4]));
m = a.selectLeaves;
tie(m[0..1]);
tie(m[4..]);
a.doLogicalTies({ |each| each[0].attach(FoscArticulation('>')) }, pitched: true);
a.show;
::
IMAGE::img/iteration-2.png::

Iterate over runs, attach slur to each run.
CODE::
a = FoscStaff(FoscLeafMaker().(#[60,62,64,nil,65,nil,67,69], [1/8]));
a.doRuns { |run| if (run.size > 1) { run.slur } };
a.show;
::
IMAGE::img/iteration-3.png::


SECTION:: License
EMPHASIS::Fosc:: is free software available under Version 3.0 of the GNU General Public License.